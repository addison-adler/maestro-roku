import "pkg:/source/game/GameEnums.bs"

namespace mg
  class GameItem

    index as integer
    isCollected = false
    itemType = mg.GameItemType.None

    function new(index as integer, itemType as mg.GameItemType)
      m.index = index
      m.itemType = itemType
    end function

    function reset()
      m.isCollected = false
    end function

    function getCollision(target as mc.types.node, poster as mc.types.node) as object
      if poster = invalid
        return mg.Collision.None
      end if
      translation = poster.translation
      size = poster.size
      size[1] = 20 'only first 20 pixels of floor count
      targetSize = [target.width, target.height]
      targetTranslation = target.translation
      parentTranslation = target.getParent().translation

      ' Calculate the absolute position of the target in the scene
      collisionTranslation = [
        targetTranslation[0] + parentTranslation[0],
        targetTranslation[1] + parentTranslation[1]
      ]

      ' Check for horizontal overlap
      horizontalOverlap = (translation[0] < collisionTranslation[0] + targetSize[0]) and (translation[0] + size[0] > collisionTranslation[0])

      ' Check for vertical overlap considering the size of both the player and the target
      verticalOverlap = (translation[1] < collisionTranslation[1] + targetSize[1]) and (translation[1] + size[1] > collisionTranslation[1])

      if horizontalOverlap and verticalOverlap
        return mg.Collision.Inside
      end if

      return mg.Collision.None
    end function

    private function getUri() as string
      uri = ""
      if m.itemType = mg.GameItemType.coin
        uri = "pkg:/images/sprites/coin-tile.png"
      else if m.itemType = mg.GameItemType.Enemy1
        uri = "pkg:/images/sprites/bad-guy.png"
      else if m.itemType = mg.GameItemType.Enemy2
        uri = "pkg:/images/sprites/bad-guy.png"
      else if m.itemType = mg.GameItemType.Spikes
        uri = "pkg:/images/sprites/bad-guy.png"
      else
        ? " UKNOWN GAME ITEM!!"
      end if
      return uri
    end function

    private function getEffect() as mg.GameItemEffect
      effect = mg.GameItemEffect.None
      if m.itemType = mg.GameItemType.coin
        effect = mg.GameItemEffect.Coin
      else if m.itemType = mg.GameItemType.Enemy1
        effect = mg.GameItemEffect.Kill
      else if m.itemType = mg.GameItemType.Enemy2
        effect = mg.GameItemEffect.Kill
      else if m.itemType = mg.GameItemType.Spikes
        effect = mg.GameItemEffect.Kill
      else
        ? " UKNOWN GAME ITEM!!"
      end if
      return effect
    end function


  end class

end namespace
