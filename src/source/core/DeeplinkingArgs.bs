import "pkg:/source/roku_modules/log/LogMixin.brs"

namespace mc

  interface IComponentTypeArgs
    contentId as string
    mediaType as string
  end interface

  @strict
  @node("mc_DeeplinkingArgs", "Node")
  class DeeplinkingArgs
    '(none|movie|short-form|live|special|episode|series|season)
    public mediaType = "none"
    public contentId = ""
    private log = new rlog.Logger("DeeplinkingArgs")
    private top as roSGNode

    'updates the args with the passed in AA, and updates the version count

    'returns the required action to fulfil the current deeplinking
    '(none|play|display)

    function configure(args as IComponentTypeArgs)
      m.contentId = ""
      m.mediaType = "none"
      m.log.info("configuring with args", args)

      supportedTypes = {
        "season": true
        "movie": true
        "short-form": true
        "live": true
        "special": true
        "episode": true
        "series": true
      }

      if args <> invalid and args.mediaType <> invalid and args.mediaType.trim() <> "" and args.contentId <> invalid and args.contentId.trim() <> "" and supportedTypes[args.mediaType] = true
        m.contentId = args.contentId.DecodeUriComponent()
        m.mediaType = args.mediaType
      end if
    end function

    function getAction(args = invalid as roAssociativeArray) as string
      'bs:disable-next-line
      m.log.info("getAction mediaType", m.mediaType)

      action = "none"

      displayActions = {
        "season": true
      }

      playActions = {
        "movie": true
        "short-form": true
        "live": true
        "special": true
        "episode": true
      }

      playAndDisplayActions = {
        "series": true
      }

      if playAndDisplayActions[m.mediaType.trim()] = true
        action = "playAndDisplay"
      else if displayActions[m.mediaType.trim()] = true
        action = "display"
      else if playActions[m.mediaType.trim()] = true
        action = "play"
      end if

      m.log.info("results in action", action)

      return action
    end function
  end class
end namespace
