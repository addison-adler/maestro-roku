import "pkg:/source/ml/ItemScroller.bs"
import "pkg:/source/ml/RowItemViewManager.bs"

namespace ml
  class RowItemScroller extends ml.ItemScroller

    public viewManager as ml.RowItemViewManager


    function new(viewManager as ml.RowItemViewManager, focusManager as roSGNodeRowList)
      super("RowItemScroller", viewManager, focusManager)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ overridden
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    override function getFocusSettings()
      'FIXME roSGNodeml_ListContent cast should not be needed
      return ml.listMixin.getFocusSettings(m.viewManager.owner.content as roSGNodeml_ListContent, m.viewManager.content as roSGNodeml_RowContent)
    end function

    protected override function getCellInfo(cellInfo as ml.CellInfo, isFromListScroll = false as boolean) as ml.CellInfo
      if isFromListScroll
        return m.viewManager.cellInfos[m.index]
      else
        if cellInfo = invalid
          return invalid
        end if
        return m.viewManager.cellInfos[cellInfo.index]
      end if
    end function

    protected override function getFocusIndicatorTranslation(info as ml.CellInfo, cellInfo as ml.CellInfo, direction as integer, isFloatingFocus as boolean) as object
      if info = invalid
        return [0, 0]
      end if

      if m.viewManager.settings = invalid
        m.log.warn("viewManager has k settings")
        return [0, 0]
      end if

      screenPos = info.getScreenPosition(direction)

      contentOffset = m.viewManager.settings.contentOffset
      headerOffset = m.viewManager.settings.headerSettings.height

      ' ? "GFC " ; m.__classname ; " HO "; headerOffset ; formatJson(contentOffset); " SP " ;screenPos ; "iFF "; isFloatingFocus

      translation = [screenPos, 0]

      translation[1] += headerOffset

      translation[1] += m.viewManager.settings.screenPos

      if isFloatingFocus
        cell = m.viewManager.getRenderer(info.contentIndex)
        'bs:disable-next-line
        bounds = cell.sceneBoundingRect()
        translation = [bounds.x, bounds.y]
      end if

      if m.settings.horizAnimStyle = "fixed"
        translation[m.axes] = contentOffset[m.axes]
      end if

      return translation
    end function

  end class
end namespace
